Program:
    Statements [eof]
    ;

Statements:
    (Statement [semi])*
    | [semi]
    ;

Statement:
    AssignmentStatement
    | IfStatement
    | WhileStatement
    | PrintStatement
    | BlockStatement
    ;

AssignmentStatement:
    [id] [assign] Expression
    ;

PrintStatement:
    [k_print] Expression
    ;

IfStatement:
    [k_if] [lpar] Expression [rpar] Statement
    ;

WhileStatement:
    [k_while] [lpar] Expression [rpar] Statement
    ;

BlockStatement:
    [lbrace] Statements [rbrace]
    ;

Expression:
    RelationalExpression
    ;

RelationalExpression:
    AdditiveExpression ( ([lt] | [gt] | [eq]) AdditiveExpression )*
    ;

AdditiveExpression:
    MultiplicativeExpression ( ([add] | [sub]) MultiplicativeExpression )*
    ;

MultiplicativeExpression:
    PrimaryExpression ( ([mul] | [div]) PrimaryExpression )*
    ;

PrimaryExpression:
    Literal
    | [id]
    | [lpar] Expression [rpar]
    ;

Literal:
    [number]
    ;
